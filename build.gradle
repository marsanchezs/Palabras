// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'
}

plugins {
    id 'com.android.application' version "$androidGradlePlugin" apply false
    id 'com.android.library' version "$androidGradlePlugin" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id "io.gitlab.arturbosch.detekt" version "1.17.1"
}

dependencies {
    detektPlugins rootProject.detektFormattingPlugin
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion rootProject.androidCompileSdkVersion

                defaultConfig {
                    minSdkVersion rootProject.androidMinSdkVersion
                    targetSdkVersion rootProject.androidTargetSdkVersion
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility rootProject.javaVersion
                    targetCompatibility rootProject.javaVersion
                }

                kotlinOptions {
                    jvmTarget = rootProject.javaVersion
                }
            }
        }
    }
}

detekt {
    toolVersion = rootProject.detektVersion
    config = files("$projectDir/config/detekt/detekt.yml")
    buildUponDefaultConfig = true
    input = files("$projectDir/security/")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
